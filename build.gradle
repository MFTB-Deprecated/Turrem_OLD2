configure(subprojects) 
{
	def n = it.path
	def libs = [:]
	if (n == ':utils')
	{
		apply plugin: 'java'
		apply plugin: 'eclipse'
		
		libs.guava = true
		libs.gson = true
		libs.trove = true
		libs.log4j = true
	}
	else if (n ==~ ':(app|mod|coremod):(client|common|server)')
	{		
		apply plugin: 'java'
		apply plugin: 'eclipse'
		
		def partname = n.substring(1,n.lastIndexOf(':'))
		def sidename = n.substring(n.lastIndexOf(':') + 1)
		
		def isApp = partname == 'app'
		def isMod = partname == 'mod'
		def isCoremod = partname == 'coremod'
		
		def isClient = sidename == 'client'
		def isCommon = sidename == 'common'
		def isServer = sidename == 'server'
		
		if (isApp)
		{
			libs.guava = true
			libs.gson = true
			libs.trove = true
			libs.log4j = true
			
			dependencies 
			{
				compile project(":utils")
			}
		}
		
		if (isClient)
		{
			libs.lwjgl = true
		}

		eclipse 
		{
			project 
			{
				name = partname + "." + sidename
			}
		}
		
		if (!isCommon) 
		{
			dependencies 
			{
				compile project(":" + partname + ":common")
			}
		}
		
		if (isMod)
		{
			dependencies 
			{
				compile project(":coremod:" + sidename)
				compile project(":app:" + sidename)
				if (!isCommon) 
				{
					compile project(":coremod:common")
					compile project(":app:common")
				}
			}
		}
		
		if (isCoremod)
		{
			dependencies 
			{
				compile project(":app:" + sidename)
				if (!isCommon) 
				{
					compile project(":app:common")
				}
			}
		}
	}
	def repos = [:]
	if (libs.guava)
	{
		repos.mavcent = true
		dependencies 
		{
			compile group: 'com.google.guava', name: 'guava', version: '18.0'
		}
	}
	if (libs.gson)
	{
		repos.mavcent = true
		dependencies 
		{
			compile group: 'com.google.code.gson', name: 'gson', version: '2.3'
		}
	}
	if (libs.lwjgl)
	{
		repos.mavcent = true
		dependencies 
		{
			compile group: 'org.lwjgl.lwjgl', name: 'lwjgl', version: '2.9.1'
			compile group: 'org.lwjgl.lwjgl', name: 'lwjgl_util', version: '2.9.1'
		}
	}
	if (libs.trove)
	{
		repos.mavcent = true
		dependencies 
		{
			compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
		}
	}
	if (libs.log4j)
	{
		repos.mavcent = true
		dependencies 
		{
			compile group: 'org.apache.logging.log4j', name: 'log4j', version: '2.1'
		}
	}
	if (repos.mavcent)
	{
		repositories 
		{
			mavenCentral()
		}
	}
}